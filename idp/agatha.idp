// Who killed Agatha (https://dmcommunity.org/challenge/challenge-nov-2014/)
// Someone in Dreadsbury Mansion killed Aunt Agatha. Agatha, the butler, and Charles live in Dreadsbury Mansion, and are the only ones to live there. A killer always hates, and is no richer than his victim. Charles hates noone that Agatha hates. Agatha hates everybody except the butler. The butler hates everyone not richer than Aunt Agatha. The butler hates everyone whom Agatha hates. Noone hates everyone. Who killed Agatha?

vocabulary V {
    // There are exactly 3 persons: Agatha, Butler, Charles.
    type Person constructed from {Agatha, Butler, Charles}

    // A person can kill a person.
    // !important: "a person can kill another person" is something else: wouldn't give the solution as Agatha killed herself.
    kill(Person, Person)

    // A person can hate multiple persons.
    hate(Person, Person)
    // A person can be richer than multiple other persons.
    be_richer(Person, Person)
}

structure S : V {
}

theory T : V {
    // From voc:
    // "other"
    ! x y: be_richer(x, y) => x ~= y.

    // If a person A is richer than another person B, then is B not richer than A.
    ! x y: be_richer(x, y) & x ~= y => ~ be_richer(y, x).

    // Someone in Dreadsbury Mansion killed Aunt Agatha.
    // There is someone who kills Agatha.
    ? x: kill(x, Agatha).

    // Agatha, the butler, and Charles live in Dreadsbury Mansion, and are the only ones to live there.
    // There are exactly 3 persons: Agatha, the butler and Charles
    // -> Structure

    // A killer always hates, and is no richer than his victim.
    // All persons who kill a person, hate that person
    ! x y: kill(x, y) => hate(x, y).
    // All persons who kill a person, are not richer than that person.
    ! x y: kill(x, y) => ~ be_richer(x, y).


    // Charles hates no-one that Agatha hates.
    // There is nobody that Charles hate and that Agatha hate.
    ~ ? x: hate(Charles, x) & hate(Agatha, x).

    // Agatha hates everybody except the butler.
    // Agatha hates every person that is not the butler.
    ! x: x ~= Butler => hate(Agatha, x).

    // The butler hates everyone not richer than Aunt Agatha.
    // The butler hates every person that is not richer than Agatha
    ! x: ~ be_richer(x, Agatha) => hate(Butler, x).

    // The butler hates everyone whom Agatha hates.
    // The butler hates every person is hated by Agatha.
    ! x: hate(Agatha, x) => hate(Butler, x).

    // Noone hates everyone.
    // No person hates every person.
    ~ ? x: !y: hate(x, y).
}

query Q : V {
    // Who killed Agatha?
    {k: kill(k, Agatha)}
}

procedure main() {
    stdoptions.nbmodels = 10;
	printmodels(modelexpand(T,S))
    model = modelexpand(T,S)
    print(query(Q, model[1]))
}
